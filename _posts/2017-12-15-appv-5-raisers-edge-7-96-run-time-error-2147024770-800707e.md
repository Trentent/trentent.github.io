---
id: 2632
title: AppV 5 - Raiser's Edge 7.96 - Run-time error -2147024770 (800707e)
date: 2017-12-15T12:00:48-06:00
author: trententtye
layout: post
guid: http://theorypc.ca/p=2632
permalink: /2017/12/15/appv-5-raisers-edge-7-96-run-time-error-2147024770-800707e/
image: /wp-content/uploads/2017/12/blackbaud.png
categories:
  - Blog
tags:
  - AppV
  - procmon
  - Registry
  - scripting
---
We are in the process of upgrading Blackbaud's Raiser's Edge to 7.96 and we encountered an error:

<img class="aligncenter size-full wp-image-2633" src="/wp-content/uploads/2017/12/blackbaud.png" alt="" width="295" height="163" /> 

Run-time error '-2147024770 (8007007e)':  
Automation error  
The specified module could not be found.

This error is giving us a few clues as to what might be happening. The most obvious message is the "8007007e" which is a standard windows error hex code which translates to:

8007007E = FileNotFoundException

So RE7.exe is not finding a file it's looking for. With most AppV packages we can suss out the file it's missing by using procmon and tracing for "FILE NOT FOUND" in the result field. Unfortunately, my searching for this message did **_NOT_** result in finding a file that wasn't resolved by another path. In other words, all files were accounted for. But the error message very clearly states that a file is missing. So the next step is to install the application locally and compare the launch differences between the local install and the AppV install. Again, process monitor makes this easy by using the "loaded modules" option.

The differences I found between a local install of this application and the AppV launch looked like so:

<img class="aligncenter size-full wp-image-2634" src="/wp-content/uploads/2017/12/LoadedModules.png" alt="" width="1553" height="716" srcset="/wp-content/uploads/2017/12/LoadedModules.png 1553w, /wp-content/uploads/2017/12/LoadedModules-300x138.png 300w, /wp-content/uploads/2017/12/LoadedModules-768x354.png 768w" sizes="(max-width: 1553px) 100vw, 1553px" /> 

The launches were identical, until the highlighted points. The local install, which works without issue, has an extra file that gets loaded. bbcor7.dll.

It appears, somehow, this file is getting loaded and registered dynamically on a local install, but this is not happening with the AppV install. I don't see the file get searched for at all with the AppV install and tracing with procmon. However, executing a regsvr32 /s "C:\Program Files (x86)\Blackbaud\The Raisers Edge 7\DLL\bbcor7.dll" during sequencing _does_ do all the necessary work to register and allow RE7.exe to find and load the file in an AppV bubble.

So, long story short, execute:

```shell
regsvr32 /s "C:\Program Files (x86)\Blackbaud\The Raisers Edge 7\DLL\bbcor7.dll
```


While sequencing your AppV package and this should fix this issue.

Here is my entire sequencing script:

```shell
:: Raiser's Edge Install
:: Trentent Tye
:: PackageName: Blackbaud_RaisersEdge_7.96.6401_x86

:: Need Visual Basic for Applications (VBA) installed *BEFORE* sequencing or else it will conflict with
:: locally installed VBA with Office 2010 on the vDisk.  This causes 'ActiveX components creation failed' along with
:: other errors.
:: "Visual Basic for Applications Version 2627.01\vba6.msi"
:: Also note that you may get a 'ActiveX components creation failed' or such error message if you launch RE_SAMPLE_7
:: database.  This is normal and you do not experience this error message in prod.

:: Install prerequistes (to be captured in package now!!!)
pushd %~dp0
msiexec.exe /i "ISSetupPrerequisites\Crystal Reports Support Pack 10\CRRuntime_32bit_13_0_10.msi" /qb

:: Install Raiser's Edge
"setup.exe" /s /v"/qb"

:: Silently install BBPatch.exe  --> need to be in the same directory as BBPatch.exe or it errors...
cd /d "Patch"
MSIEXEC /qn /P "Patch\Patchpackage.msp" REINSTALL=ALL REINSTALLMODE=emus REBOOT=REALLYSUPRESS

:: Need to add the revision number of the BBPATCH or else it will prompt to reinstall the patch...
reg add "HKEY_LOCAL_MACHINE\SOFTWARE\Wow6432Node\Blackbaud\REINI\The Raiser's Edge 7\General" /v Revision /d 5 /t REG_SZ /f

:: Copy BBUpdate.ini over top of the one in the Raiser's Edge folder.  Raiser's Edge uses this file and the previously set
:: registry key to determine if an update is required.  By adding both we tell it that we already silently installed the
:: patch.  If this program gets updated you'll need to manually install it and grab the BBUpdate.ini file from the 
:: updated version.
cd..
copy /y "______BBUpdate.ini" "C:\Program Files (x86)\Blackbaud\The Raisers Edge 7\BBUpdate.ini"

:: In order to stop "Run-time error -2147024770 (800700e)" we need to register the following dll:
regsvr32 /s "C:\Program Files (x86)\Blackbaud\The Raisers Edge 7\DLL\bbcor7.dll"

::fixes appv sequencer dieing at the end of package (invalid manifest)
reg delete HKEY_LOCAL_MACHINE\SOFTWARE\Classes\.re7 /f

:: Apply mail merge fix
regedit /s "MailMerge fix\Word2010.reg"

:cleanup directories/files
mkdir "C:\ProgramData\Microsoft\Windows\Start Menu\Programs\MyApps\Blackbaud"
mkdir "C:\Users\Public\Desktop\MyApps\Blackbaud"
move "C:\Users\Public\Desktop\The Raiser's Edge.lnk" "C:\ProgramData\Microsoft\Windows\Start Menu\Programs\MyApps\Blackbaud"
copy /y "C:\ProgramData\Microsoft\Windows\Start Menu\Programs\MyApps\Blackbaud\*.lnk" "C:\Users\Public\Desktop\MyApps\Blackbaud"
del /s /q "C:\Users\Public\Desktop\RE Release Notes.lnk"
rmdir /s /q "C:\ProgramData\Microsoft\Windows\Start Menu\Programs\The Raiser's Edge"

```

&nbsp;

<!-- AddThis Advanced Settings generic via filter on the_content -->

<!-- AddThis Share Buttons generic via filter on the_content -->
